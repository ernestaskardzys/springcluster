buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.3.RELEASE")
    }
}

apply plugin: 'java'

group 'info.ernestas'
version '1.2'

allprojects {
    sourceCompatibility = 11
    targetCompatibility = 11

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceSets {
        integrationTest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/integration-test/java')
            }
            resources.srcDir file('src/integration-test/resources')
        }
    }

    repositories {
        mavenCentral()
    }

    configurations {
        integrationTestCompile.extendsFrom testCompile
        integrationTestRuntime.extendsFrom testRuntime
    }

    dependencies {
        compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.8.0-beta2'
        compile('org.springframework.boot:spring-boot-starter-actuator')

        testCompile('org.springframework.boot:spring-boot-starter-test')
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.0-M1'
        testCompile group: 'org.mockito', name: 'mockito-core', version: '2.18.3'
    }

    task integrationTest(type: Test) {
        testClassesDirs = sourceSets.integrationTest.output.classesDirs
        classpath = sourceSets.integrationTest.runtimeClasspath
    }

    check.dependsOn integrationTest
    integrationTest.mustRunAfter test
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

wrapper {
    gradleVersion = '5.4.1'
}
